@persist Cache:array

#include "xbyte_3d_engine/3dmath"

function void wirelink:cl() {
    This:egpClear()
}

function void wirelink:poly( I, [P1 P2 P3]:vector4, C:vector4, M:string ) {
    This:egpPoly( I, P1, P2, P3 )
    This:egpColor( I, C )
    This:egpMaterial( I, M )
}

function wirelink:text( I, Text:string, Size, Font:string, Vec:vector2, C:vector4 ) {
    This:egpText( I, Text ,Vec )
    This:egpColor( I, C )
    This:egpAlign( I, 1, 1 )
    This:egpFont( I, Font, Size )
}

function void table:graphics_update() {
    local Camera = This:camera()
    local S = This:screen()
    
    local Pos = Camera:pos():invert()
    local Ang = Camera:angles():normalized()
    
    local Yaw = Ang:yaw()
    local Pitch = Ang:pitch()
    local Front = vec( cos( Yaw ) * cos( Pitch ), sin( Pitch ), sin( Yaw ) * cos( Pitch ) ):normalized() # - Center vector
    
    This["view", matrix4] = lookat( Pos, Pos + Front, vec( 0, 1, 0 ) ) # - Eye, Center, Up vectors
    
    local View = This:view() # - View matrix4
    local Viewport = This:viewport() # - Viewport matrix4
    local Project = This:projection() # - Projection matrix4
    
    local Triangles = VisibleTriangles = 0
    
    foreach( O:number, Obj:table = This:objects() ) {
        Obj["RotatedLocal", array] = array()
        Obj["Translated", array] = array()
        
        local ObjAng = Obj:obj_ang():normalized()
        local ObjPos = Obj:obj_pos():invert()
        local Model = identity4()
        
        Model = Model:translate( ObjPos ) # - Moving object
        
        Model = Model:rotate( ObjAng:roll(), 0, 0, 1 ) # - Rotating object
        Model = Model:rotate( ObjAng:pitch(), 1, 0, 0 ) # - Rotating object
        Model = Model:rotate( ObjAng:yaw(), 0, 1, 0 ) # - Rotating object
        
        Model = Model:scale( Obj:obj_scale() ) # - Scaling object
        
        local LocalPos = ( Pos - ObjPos ):normalized() # - For shading
        
        foreach( I:number, V:vector = Obj:obj_vertices() ) { 
            local Clip = Project * View * Model * vec4( V, 1 )
            local NDC = vec( Clip ) / Clip:w()
            local Position = Viewport * vec4( NDC, 1 )
            
            Obj["RotatedLocal", array]:pushVector( NDC )
            Obj["Translated", array]:pushVector( vec( Position:x(), Position:y(), Clip:w() ) )
        }
        
        foreach( I:number, V:vector = Obj:obj_tri() ) {
            local Vtx = array( Obj:obj_rl()[V:x(), vector], Obj:obj_rl()[V:y(), vector], Obj:obj_rl()[V:z(), vector] )
            local Vxyz = array( Obj:obj_tr()[V:x(), vector], Obj:obj_tr()[V:y(), vector], Obj:obj_tr()[V:z(), vector] )
            
            local Normal = ( Vtx[2, vector] - Vtx[1, vector] ):cross( Vtx[3, vector] - Vtx[1, vector] ):normalized()
            local Shading = Normal:dot( ( LocalPos - ( Vtx[1, vector] + Vtx[3, vector] ) / 2 ):normalized() )
            
            local Index = Obj:obj_index() + I
			            
			         Shading = ( Vxyz[1, vector]:z() > Camera:znear() | Vxyz[1, vector]:z() > Camera:zfar() ) | ( Vxyz[2, vector]:z() > Camera:znear() | Vxyz[2, vector]:z() > Camera:zfar() ) | ( Vxyz[3, vector]:z() > Camera:znear() | Vxyz[3, vector]:z() > Camera:zfar() ) ? 0 : Shading
            
            if( Shading > 0 ) {
                Cache[Index, normal] = 1
                
                S:poly( Index, vec4( Vxyz[1, vector]:x(), Vxyz[1, vector]:y(), 0, 0 ),
                            vec4( Vxyz[2, vector]:x(), Vxyz[2, vector]:y(), 0, 1 ),
                            vec4( Vxyz[3, vector]:x(), Vxyz[3, vector]:y(), 1, 1 ),
                            vec4( vec( sqrt( Shading ) * 255 ), 255 ),
                            Obj:obj_uv()[I, string] )
                VisibleTriangles++
            }
            elseif( Cache[Index, normal] ) {
                S:egpRemove( Index )
                
                Cache[Index, normal] = 0
            }
            
            Triangles++
        }
    }
    
    if( This["debugmode", number] ) {
        S:egpSetText( 297, "pos: " + floor( Pos:x(), 2 ) + " " + floor( Pos:z(), 2 ) + " " + floor( Pos:y(), 2 ) )
        S:egpSetText( 298, "ang: " + Ang:pitch() + " " + Ang:yaw() + " " + Ang:roll() )
        S:egpSetText( 299, "triangles: " + Triangles )
        S:egpSetText( 300, "visible triangles: " + VisibleTriangles )
    }
}

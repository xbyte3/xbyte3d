@persist Cache:array

#include "xbyte_3d_engine/3dmath"

function void wirelink:cl() {
    This:egpClear()
}

function void wirelink:poly( I, [P1 P2 P3]:vector4, C:vector4 ) {
    This:egpPoly( I, P1, P2, P3 )
    This:egpColor( I, C )
}

function void wirelink:poly( I, [P1 P2 P3]:vector4, C:vector4, M:string ) {
    This:egpPoly( I, P1, P2, P3 )
    This:egpColor( I, C )
    This:egpMaterial( I, M )
}

function void table:graphics_update() {
    local Camera = This:camera()
    local S = This:screen()
    
    local Pos = Camera:pos()
    Pos = vec( Pos:x(), Pos:z(), Pos:y() )
    local Ang = Camera:angles():normalized()
    
    local Yaw = Ang:yaw()
    local Pitch = Ang:pitch()
    local Front = vec( cos( Yaw ) * cos( Pitch ), sin( Pitch ), sin( Yaw ) * cos( Pitch ) ):normalized() # - Center vector
    
    This:lookat( Pos, Pos + Front, vec( 0, 1, 0 ) ) # - Eye, Center, Up vectors
    
    local View = This:view() # - View matrix4
    local Viewport = This:viewport() # - Viewport matrix4
    local Project = This:projection() # - Projection matrix4
    
    foreach( O, Obj:table = This:objects() ) {
        Obj["RotatedLocal", array] = array()
        Obj["Translated", array] = array()
        
        local Model = identity4()
        
        Model = Model * Model:scale( Obj:obj_scale() ) # - Scaling object
        Model = Model:translate( Obj:obj_pos() ) # - Moving object
        
        local LocalPos = ( Pos - Obj:obj_pos() ):normalized() # - For shading
        
        foreach( I, V:vector = Obj:obj_vertices() ) {
            V = V:rotate( Obj:obj_ang() )
            
            local Clip = Project * View * Model * vec4( V, 1 )
            local NDC = vec( Clip ) / Clip:w()
            local Position = Viewport * vec4( NDC, 1 )
            
            Obj["RotatedLocal", array]:pushVector( NDC )
            Obj["Translated", array]:pushVector2( vec2( Position ) )
            
        }
        
        foreach( I:number, V:vector = Obj:obj_tri() ) {
            
            local Vtx1 = Obj:obj_rl()[V:x(), vector]
            local Vtx2 = Obj:obj_rl()[V:y(), vector]
            local Vtx3 = Obj:obj_rl()[V:z(), vector]
            
            local Normal = ( Vtx2 - Vtx1 ):cross( Vtx3 - Vtx1 ):normalized()
            local Shading = Normal:dot( ( LocalPos - ( Vtx1 + Vtx3 ) / 2 ):normalized() )
                
            local Index = Obj:obj_index() + I
            
            if( Shading > 0 ) {
                Cache[Index, normal] = 1
                
                S:poly(Index, vec4( Obj:obj_tr()[V:x(), vector2], 0, 0),
                            vec4( Obj:obj_tr()[V:y(), vector2], 0, 1),
                            vec4( Obj:obj_tr()[V:z(), vector2], 1, 1),
                            vec4( sqrt( Shading ) * 255 ) )
            }
            elseif( Cache[Index, normal] ) {
                S:egpPoly( Index, vec2(), vec2(), vec2())
                
                Cache[Index, normal] = 0
            }
        }
    }
}
